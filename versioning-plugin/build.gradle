buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.novoda:bintray-release:0.8.1'
    }
}

plugins {
    id 'groovy'
    id 'java'
    id 'com.gradle.plugin-publish' version '0.9.10'
    id "net.ltgt.apt-idea" version "0.18"

}

apply plugin: 'com.novoda.bintray-release'

project.description 'Gradle project versioning plugin based on Git tags.'
project.group = 'com.chattylabs.plugin'
project.version = '0.17.1'
project.ext.name = project.name.replace("-plugin", "")

sourceCompatibility = JavaVersion.VERSION_1_8

repositories {
    jcenter()
}

compileGroovy {
    groovyOptions.javaAnnotationProcessing = true
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()

    testImplementation group: 'junit', name: 'junit', version: '4.12'
}


println 'Publish: ' +
        project.group + ':' +
        project.ext.name.toString() + ':' +
        project.version
println '- ' + project.description


pluginBundle {
    website = project.websiteUrl
    vcsUrl = project.vcsUrl
    tags = ['gradle', 'version', 'release', 'versioning', 'git']

    plugins {
        versioningPlugin {
            id = project.group + '.' + project.ext.name
            displayName = project.ext.name
            description = project.description
        }
    }
}

publish {
    userOrg = 'chattylabs'
    groupId = project.group
    artifactId = project.ext.name
    publishVersion = project.version
    desc = project.description
    website = project.websiteUrl
}
